After parting ways with Malfoy, you walk around for a while. You don't feel like going home just yet, because you fear an awkward conversation with Ron, or even worse having to tell Hermione what happened.

*if ((crest_explain) and (UST > 75))
  However, for some reason you feel restless. Your feet take you uphill, to the fancy rows of houses where the oldest and richest families in ${town} live. You've been here several times before, for work, so it isn't difficult for you to locate Malfoy's address.

  There's still a while to go before the appointed time, but you feel that Malfoy is in danger and there is no time to lose in apprehending the culprit. The front gate feels too imposing for you, so you elect to try the service entrance on the back of the house.

  You knock on the door but nobody comes to answer. Perhaps, given that it's dinnertime, the servants are all busy in the kitchen or in the dining room. You're about to knock again when the door slams open and someone roughly shoves you out of the way.

  *page_break

  You stumble and fall down a couple of steps, barely catching your balance. Two men run out of the house, carrying a limp body between them. You run after them, but you barely have time to notice Malfoy's blond hair before the two men disappear into a carriage that was waiting for them behind the corner.

  Several shouts sound behind you, as several members of the household rush out into the street. They're running after the kidnappers, but they come too late. You, however, might still catch up with them.

  The stables are adjacent to the town house, and you hear a horse whinnying. Before you can think better about it, you've jumped on a bay mare and you're galloping down the street after the black carriage.

  *page_break

  You hear someone yelling "Stop! Kidnappers!" but it quickly fades away in the distance. The carriage has a good lead on you, but your mare is fast and even when you lose sight of your target it's easy to follow the telltale clicking sound of the wheels and the hoofbeats of the black horses.

  Your chase takes you through several districts of ${town}, until the houses start to thin and you're racing along a moonlit country lane. Maybe the driver thought that he could shake you off on a long stretch of road, but the four black horses are encumbered by the carriage and its passengers. Your mare only has your weight to carry, and you're quickly gaining ground.

  Finally the mare catches up with the carriage.

  *page_break

  You glance inside the carriage and you see Malfoy slumped in the seat next to a woman dressed in black. Bellatrix, Lady Lestrange, sees you too and yells something to the coachman. He tries to spur the horses to go faster, but you jump from your mare and you knock him from his perch with a punch. The coachman tumbles from the carriage and into a ditch. You catch the reins just in time and stop the horses before they overturn the carriage in their agitation.

  You turn around just in time to see Lady Lestrange's two henchmen jumping down from the carriage and coming at you. You're in no mood to lay around, though, and those two thugs present no challenge for you. You dispatch them in a few seconds and you leave them half-stunned in the middle of the road.

  "Come out!" you call. "It's over, Bellatrix Lestrange."

  *page_break

  She comes out of the carriage, looking as proud as any queen, holding her nephew in front of her at ${weapon}spoint.

  "The duelist," she says, her mouth curling in an expression of distaste. Malfoy has taken to saying the word with a mixture of mockery and exasperated fondness. When Bellatrix Lestrange says it, you can only feel the hatred. This is a woman who hates the whole world. "From the moment I knew about your involvement with Draco, I knew you'd be trouble."

  You can't attack her while Malfoy is in the way. If she still has some shreds of honour left she might accept a formal challenge to a duel. Or you could try to keep her talking in the hope that she will lower her guard.

  *choice
    #Challenge her
      "Lady Lestrange," you say, holding out your sword. "I challenge you to a formal duel."

      It's a long shot. She has the upper hand here, and no reason to accept.

      Her answer is a shrill laughter that takes you by surprise. "The duelist challenges me to a duel!" she says, almost bent double with laughter. "You must be very stupid, or very desperate, to try that." Then she straightens herself. "Why not? I'd like to measure myself against the best duelist in ${town}."

      You grip your ${weapon} with fingers that are suddenly sweaty. "So you accept?"

      "I accept your challenge, Potter," she says with a nasty grin. "First, I'll kill you in front of darling Draco. And then I'll kill [i]him[/i]."

      *page_break

      She shoves Malfoy aside and you take your positions the field next to the road. You stare at each other across the distance.

      "It's customary to bow before a duel," she says.

      You incline your head, just a fraction, and in that moment she strikes. You raise your ${weapon} just in time to parry her ${weapon2} and in that moment you realize you have made a mistake in thinking that she would play by the rules. But you've made an even bigger mistake in thinking that she would be as easy to defeat as any other nobleman.

      Bellatrix Lestrange is good. She's better than most professional duelists you've faced. Merlin, you think as she slashes at you and just barely misses cutting off your arm, she's better than any of the the professional duelists you've ever faced in your life. The question is whether she's better than you.

      *page_break

      You will yourself to forget all about Malfoy. You try to slip into that state of mind in which nothing matters, in which your ${weapon} is an extension of your own body.

      It doesn't work. Unbidden, your eyes keep straying towards Malfoy, who's leaning against the carriage for support and watching the duel with wide eyes.

      *if Weapon="sword"
        Bellatrix takes advantage of your distraction, locking your blade with her own and then kicking you in the stomach hard enough to make you double over in pain.

      *if Weapon="wand"
        Bellatrix takes advantage of your distraction, hitting you in the gut with a powerful curse that leaves you doubled over in pain.
        
      Your eyes stray towards Malfoy again, and she laughs. "You really care about my idiot nephew," she says. "I've changed my mind: I'll kill him first, and then we can finish our duel without further distractions."

      Malfoy makes a choked sound as his aunt turns towards him and raises her ${weapon}...
      *choice
        #Shield Malfoy with your body
          With your last shreds of energy you push yourself to your feet and throw yourself over Malfoy. However, it's not enough: Bellatrix's ${weapon2} pierces both your body and Malfoy's.

          The two of you fall together in a tangle on the grass. As your consciousness fades, you're painfully aware of your own failure.

          You, Harry Potter, are dead. You will not find out what happens to Bellatrix, or to Ron and Hermione, or to anyone else. You will never know that Narcissa Malfoy, in a rare display of sentimentality, asked for you to be buried next to her son. Your body will forever rest near that of the man that you died to protect.

          [b]GAME OVER[/b]
          *ending
        #Push Malfoy out of the way
          With your last shreds of energy you push yourself to your feet and throw yourself at Malfoy. You push him out of the way just in time: you hear the [i]whoosh[/i] of Bellatrix's ${weapon2} and a sharp pain shots through your arm.

          You lay facedown in the grass, half sprawled over Malfoy and too dazed to move. Even when you hear Bellatrix approaching, you can't manage to get up. You don't even know where your ${weapon} is, you've lost it somewhere during your fall.

          Over you, Bellatrix Lestrange tut-tuts and shakes her head. "What a pitiful end for the famous Harry Potter," she says. She moves to give you the finishing blow, and that's when Malfoy ${hexes} her with your ${weapon}.
          *goto true_end
    #Distract her by talking
      "What do you mean?" you ask, stalling for time. "I know you don't approve of Malfoy dropping out of University and living a wasteful life, but that started long before he met me."

      Bellatrix Lestrange snorts. "You have no idea what you're talking about!" she exclaims. "None whatsoever! What do I care about University? That's just a bunch of old greybeards, arguing about Latin and semantics. It was his father who'd pushed into applying in the first place. I was glad when he dropped out!"

      As she talks, she's getting more and more agitated, and slackening her grip on Malfoy. Malfoy moves his lips soundlessly. "Keep talking," he mouths.

      "So... what did you want him to do with his life?" you ask.

      The noblewoman's eyes lit up. "I was going to sponsor his entrance into politics!" she exclaims. "This country is on the brink of disaster. There is talk, dangerous talk, about letting commoners into ${parliament}. This can't be allowed to continue! We must protect the monarchy!"

      *page_break

      You stare at her in disbelief, almost forgetting about your goal to distract her. "You'd kill Malfoy to protect the monarchy?" you ask.

      Bellatrix Lestrange looks as if her tenuous grip on sanity is slipping away. "He's the symbol of all that's rotten with our society!" she spits out, throwing a venomous glance at her nephew. "Instead of taking your rightful place among the elite, you run off to cavort with lowborn scum like him," she yells in Malfoy's face. "I could hardly believe it when I was told that you'd taken a filthy commoner to your bed! I'd rather see my line die out rather than have someone like you in my ancestral home."

      "We have that in common," Malfoy says, his voice barely shaking. "I also would rather die rather than live in that stuffy old mausoleum."

      You see your chance and, while she's turned away from you, you strike. She steps aside just in time to avoid your attack, and counters. Her ${weapon2} hits your arm and leaves you bleeding from a long cut.

      But the distraction has been enough for Malfoy to free himself from his aunt's grasp. He wrestles her ${weapon} away from her, and before she can react he ${hexes} her with her own ${weapon}.
      *goto true_end

At the appointed hour, you show up on the step of the Malfoys' fancy town house. It's an imposing building with a marble facade, and under other circumstances you'd use the service entrance at the back. But you were invited personally by Malfoy, so you feel you have a right to go through the front door.

You knock several times but nobody answers; you can hear raised voices and there seems to be some sort of commotion going on inside. You try pushing the door and it swings open.

Inside, the house is in an uproar. Servants are running around, many of them holding cudgels and other blunt weapons. Some are carrying broken pieces of furniture out of the room. In the middle of this commotion stands a tall, regal woman who's giving out orders. The family resemblance is unmistakeable.

"Lady Malfoy," you say, stepping towards her.

*page_break

Seeing you approach, Lady Malfoy points her slim silvery ${weapon} at you.

"Stop there," she says in an imperious manner. "Where have your accomplices taken my son?"

Her words send a surge of panic through you and your answer comes out sharper than you meant it. "I have no idea what you're talking about. You mean to say that Malfoy has been taken?"

"You're the duelist," she says, as realization dawns on her. She lowers her ${weapon}, though she keeps glaring at you. "Two masked men broke in only a few minutes ago. Draco tried to fight them off, but they stunned him and carried him away. I was in the middle of organizing a search party." She narrows her eyes, the gesture eerily similar to her son's.
*if (Friendship > 50)
  "If you're really Draco's [i]friend[/i] like he claims," she says, speaking the word with distaste, "do you have any idea who might have taken him?"
*if not(Friendship > 50)
  "I know you and Draco don't get along, but if you have any idea who might have taken him, then speak now."

*page_break

You think back on your conversation with Malfoy and wish he'd voiced his suspicions. The black carriage that you saw earlier is surely connected to the kidnapping, but you don't know how this could help you find Malfoy.

"I don't know who took him," you say. "But there's someone who might..."

Who can help you find Malfoy before it's too late?

*choice
  #Lady Malfoy
    "You, Lady Malfoy," you tell her. The black carriage had some significance for Malfoy, so his mother might recognize it as well. You describe the events of the evening in as much detail as you can.

    Lady Malfoy is skeptical at first, but when you tell her about the crest on the carriage and about the lady who hired Ron, she presses her mouth into a thin line.

    "Do you know who this person might be?" you ask, and you see her visibly steeling herself for control.

    She nods, just once. "My sister," she says in a tightly controlled voice. "Lady Bellatrix Lestrange."
    *goto bellatrix_villa
  #Ron
    You rush back to ${home} hoping to find Ron at home. He's your best friend and the person you trust the most. Recent events have not changed that.

    Hermione is behind the counter at the ${pub}. "Harry!" she exclaims as you come in. "What's going on? Ron came back just now, all covered in bruises. He won't tell me what happened! Please tell me he didn't start dueling again..."

    She looks so worried, and you wish you could reassure her on that score. "I'll explain everything later," you promise. "But right now I need to talk to Ron."

    Ron is upstairs, tending to his wounds with some medicinal ointment and with a lot of booze. He doesn't seem very inclined to listen to you and he keeps complaining that you're throwing him over in favour of your new rich best friend.

    *page_break

    Finally you manage to persuade Ron that Malfoy is in danger, and you ask him about the mysterious lady all dressed in black who hired him. Even then, he's not much help. All he can tell you is that the lady had a posh accent, that she was dressed in a black mourning dress, and that she had a hat with a veil that covered her face. When you ask him about the crest on the carriage, Ron says he didn't pay any attention to it.

    After almost one hour, you realize that this is a fruitless endeavour: Ron doesn't know anything.

    You return to the Malfoy town house and you join one of the search parties. You scour the city all night, but to no avail. The next day you get word that Malfoy's body has just been found floating in the river.

    You, Harry Potter, will never be able to forgive yourself for Malfoy's death. You will spend the rest of your life wondering what you could have done or said differently, what might have led you to Malfoy in time to save him. Even the knowledge that Lady Malfoy later discovered the identity of the culprits, and that she's making sure that they'll hang for their crime, will not comfort you. Ron will never forgive you for stopping his duel with Malfoy, and after finding out about it Hermione won't forgive either of you.

    [b]GAME OVER[/b]
    *ending
  #Hermione
    You rush back to ${home} hoping to find Hermione at home. She's the smartest person you know, and if she can't figure this out then nobody will.

    Thankfully she's behind the counter at the ${pub}. "Harry!" she exclaims as you come in. "What's going on? Ron came back just now, all covered in bruises. He won't tell me what happened! Please tell me he didn't start dueling again..."

    She looks so worried, and you wish you could reassure her on that score. "I'll explain everything later," you promise. "But right now I need your help."

    You tell her about the events of the evening, describing the crest on the carriage that you saw in as much detail as you can. As you talk, she has a thoughtful look on her face. "With a skull on top of the crest," she repeats, nodding, as if that if somehow significant.

    "Do you know who this 'lady all in black' might be?" you ask.

    She nods. "I heard that she's the last of her family, so nobody else uses that crest. There's only one person the carriage could belong to: Malfoy's own aunt, Lady Bellatrix Lestrange."
    *goto bellatrix_villa

*label bellatrix_villa

*page_break

"Where could she have taken him?" you ask

"She has a villa just outside ${town}," she replies. "Take our horse, it will be faster than a carriage.

You hope and pray that she's right, and that you can reach Malfoy before it's too late.

"Go to the ${auror}s," you call over your shoulder as you run out of the door. "Have them send a rescue party to this villa."

But you know that official help will take too long, and you can't count on them to get there on time. It's up to you to save Malfoy... and you don't want to think about the possibility of failure.

*page_break You ride towards the villa...

It has started to rain by the time you reach the Lestrange estate a few miles out of ${town}. The villa looks almost abandoned from the outside. There is ivy climbing all over the building and the flowerbeds are overgrown with weeds.

The black carriage is parked in the driveway. You approach it with your ${weapon} raised but no sound comes from inside. When you fling open the door of the carriage, you see two dead men sprawled on the seats.

For a moment you feel a surge of hope, thinking Malfoy has overpowered his captors and escaped. But there are no signs of a struggle. You think it's more likely that Bellatrix Lestrange killed her two henchmen for some reason and then took Malfoy inside the house. At any rate, there are muddy footprints leading up the front steps.

You follow the tracks inside the house.

*page_break

There are lit candles here and there. They give you just enough light to follow the muddy footprints to the house. There is one set of shoe prints, and then drag marks. You don't want to think about what that might mean.

The marks stop in front of the door to the cellars. You open it, but you haven't been careful enough. The door's rusty hinges give out a loud creak and Bellatrix Lestrange turns around to look at you.

"The duelist," she snarls. "You should have stayed out of this, boy!"

She's standing at the bottom of the stone stairs, in the middle of the cavernous cellar. Next to her, Malfoy is unconscious and tied to one of the many stone pillars supporting the ceiling. When Lady Lestrange moves, you hear a splashing sound, and you realize that there's several inches of water covering the floor of the cellar.

"Step away from Malfoy," you say, slowly making your way down the stairs.

*page_break

"Have you come to rescue my hopeless nephew once again?" she asks. "Don't you have anything better to do?"

"I won't let you kill him," you say.

At that, she laughs. "Don't worry, Potter, I don't plan on killing him," she says. There's a manic glint in her eye. "All I want to do is to protect the purity of my family, so it would hardly do for me to kill my own nephew. No, the water will do that for me."

You look at the water all around her and are dimly aware of a rushing sound coming from the far end of the room. She nods, smiling like a lioness preparing to pounce.

"Those cellars are connected to the river and flood with the tide," she says. "My ancestors used them to smuggle goods downriver, but they can also be useful for disposing of useless nephews..."

*page_break

You've been creeping closer while she talks, and as you reach the bottom of the stairs you try to take her by surprise with a swift attack.

But she was expecting you and she laughs as she blocks your ${weapon2}. "Come at me if you dare," she taunts you. "I'd like to measure myself against the best duelist in ${town}!"

You trade blows, wading knee-deep in the rising water. With dismay, you realize that Bellatrix Lestrange is good. She's better than most professional duelists you've faced. Merlin, you think as she slashes at you and just barely misses cutting off your arm, she's better than any of the the professional duelists you've ever faced in your life. The question is whether she's better than you.

The water is making your movements sluggish and you can't move your legs as quickly as you're used to. Your ${weapon2} misses her by a mile and you lose your footing on the slippery floor, falling forward in a crumpled heap. Your eyes go wide as you realize that you're exposed and you brace for Bellatrix's counterattack.

*page_break

Instead, she laughs and turns away. "I'd like to stay and play a bit more, but this place is too damp for my tastes," she says. "Besides, I expect the ${auror}s will come calling soon and I'd rather not be here when they arrive."

Her quick strides carry her up the stairs before you can get back on your feel.

You consider the situation. You need to decide what to do, and you need to decide now. The tide is coming in, and it might be a matter of minutes before the whole cellar is underwater; Malfoy is still unconscious and tied to a pillar. However, if you stop to help Malfoy, Bellatrix Lestrange will get away; you don't trust the ${auror}s to be able to track her down if she leaves ${town} for good.

*choice
  #Go after Bellatrix
    Bellatrix Lestrange can't be allowed to get away with this! You consider that you have enough time to stop her before the water reaches Malfoy's mouth and nose.

    You rush up the stairs through the corridors of the villa, following the sound of her footsteps. You catch up with her in the courtyard as she's trying to saddle one of the horses.

    This time there is no creaking door to give you away, and you take full advantage of your surprise attack, packing enough force into your blow that she's knocked off her feet.

    You point your ${weapon} towards her heart. "It's over," you say.

    *page_break

    As you tie her up, she can't stop laughing. "Yes, it's over," she keeps repeating. "It's too late, it's over."

    You think the shock of her plan going awry has unhinged her completely, but when you run back towards the cellar you find that the water has almost reached the arched ceiling.

    You have badly miscalculated the time it would take for the water to fill the cellar, and Malfoy has paid the price.

    You, Harry Potter, will never fight another duel in your life. The events of today will cause you to lose all your nerve, and you will never be able to make a decision without second-guessing yourself. For many years your dreams will be haunted by Bellatrix Lestrange's voice, and you will be taunted by the knowledge of your failure. She will spend her life in prison, but in a way she has accomplished her goal. She will be the winner.

    [b]GAME OVER[/b]
    *ending
  #Rescue Malfoy
    *if (Friendship > 50)
      The thought of Bellatrix Lestrange getting away fills you with rage, but Malfoy is your friend. You will not let him drown.

    *if not(Friendship > 50)
      The thought of Bellatrix Lestrange getting away fills you with rage, but even though you hate Malfoy you can't let him drown.

    You wade through the water and you grab Malfoy by the front of his shirt. "Malfoy!" you call, shaking him. His head lolls to one side, but he groans. "Malfoy, wake up!"

    There is no answer.

    *if Weapon="sword"
      Using your sword, you start hacking at the rope tying him to the pillar. It's a sturdy rope, the kind sailors use, and your sword is more suited to stabbing a man through the heart rather than this.
    *if Weapon="wand"
      You cast a severing spell on the rope that's tying Malfoy to the pillar, but there is no effect. You guess that Bellatrix has strengthened the rope with magic.
    Still you persist in your attempts, and eventually you notice that the rope has begun to fray.

    By now, the water is reaching up to your chest. It's coming more quickly now, you don't have much time left.

    *page_break

    With one last pull, the rope breaks. Malfoy slumps forward and you catch him in your arms.

    He groans something that sounds like a question, but you have no time for that now. You manage to half drag him, half push him up the stone stairs and on to higher ground. By the time you stumble out of the cellar, Malfoy is able to stand on his own legs, albeit only with your support.

    You collapse just outside the cellar door, and you turn around to watch the swirling water that's reaching almost up to the vaulted ceiling now. This was a very close call.

    You do a quick sweep of the house, just in case Bellatrix hasn't left yet, but you have no luck: she has taken one of the horses and ridden out into the storm long ago.

    *page_break

    Having made sure that the villa is safe, you check on Malfoy. He's shivering and his skin is clammy to the touch, but he doesn't seem harmed in any other way. However he's in no shape to ride back to ${town}, and the dead bodies inside the carriage make you unwilling to use that.

    You're both dripping wet, so you light a fire in one of the rooms and you sit together on the dusty carpet, as close to the fireplace as you can without getting burned. And still Malfoy keeps shivering.
    *if (Friendship > 50)
      You give him your cloak to keep warm and he burrows gratefully into it.

    "What happened?" Malfoy asks you. "The last thing I remember is being attacked in my own home..."

    You fill him in on everything, including the fact that it was his aunt who had orchestrated first his duel with Ron and then his kidnapping. He doesn't seem surprised by this, confirming your idea that he'd already guessed about Bellatrix's involvement.

    "But I'm afraid we won't catch her," you conclude. "She's long gone."

    *page_break

    "Does my mother know?" Malfoy asks. "She might know where to find Aunt Bella."

    You nod, even though you're not as confident as he sounds. "The ${auror}s have been alerted too."

    *if (Friendship > 50)
      He wraps his arms around his knees. "Thank you," he says in a muffled voice. "For saving my life. Again." His face is half-hidden in the shadows, but you think there's a blush spreading on his cheeks that has nothing to do with the warmth of the fire.

    *if not(Friendship > 50)
      He wraps his arms around his knees and pouts. "It's your fault for letting her get away," he mutters.

      "Would you rather I'd gone after her and let you drown?" you snap back, and he has the grace to blush.

    *page_break
    *goto choose_ending

*label choose_ending

*if (UST > 55)
  You find yourself staring at the play of firelight on Malfoy's face.
  *if (Friendship > 50)
    You've always thought him handsome, but there's something softer about him tonight. He catches you staring, and without a word he lifts a corner of the cloak so you can both snuggle under it.

    You curl up together, looking at the flames in silence, and you run your hands up and down his arms for warmth. You keep holding him long after he's stopped shivering.

    It's just for warmth, you tell yourself, but then Malfoy turns his face towards you, looking expectant, lips slightly parted. You're not sure what this means, you're never sure of where you stand when dealing with Malfoy, but you want this so much.

    When you kiss him, he kisses you back. You kiss Malfoy desperately, terrified by how close you were to losing him without even making him yours.

    You don't say anything, but you know he feels the same. He clings to you as if he's still drowning.
    *goto good_end
  *if not(Friendship > 50)
    You've always thought him handsome, but you wish there was a way to shut Malfoy up. "I don't know why I bother with you," you growl.

    He gives you an amused look. "Don't you, Potter?" he drawls.

    And you do know, you really do know why, which is the most infuriating part. "Fuck you, Malfoy," you say as you hear your blood rushing in your ears.

    "You'd like that, wouldn't you?" he replies, licking his lips, and that's the last straw.

    You don't know if you imagined the glint of pure want in his eyes, but you push him down, pinning him to the floor and crushing your lips together.

    He tangles his fingers in your hair and pulls you closer, deepening the kiss. That's all the confirmation you need: you might not know what's going on, but it's definitely mutual. And, for once, Malfoy doesn't say anything for a long time aside from your name.
    *goto good_end

After a while, Malfoy falls asleep, leaning against you for warmth. You stay awake and on your guard all night: you're still strung up from everything that happened and you can't discard the possibility that Bellatrix will come back.

A couple of hours before daybreak, the cavalry finally shows up. Lady Malfoy has roused the whole of ${parliament} and sent search parties all over. Lady Malfoy herself rode out to the Lestrange villa in elegant carriage, now quite drenched with rain.

There is no public display of affection, because nobles are not supposed to hug each other in public, but you don't miss the look of relief on her face when she sees her son, or the lump in Malfoy's throat as he assures her that he is all right.

*page_break Epilogue

The ${auror}s keep scouring ${town} and the surrounding countryside, but Bellatrix Lestrange manages to evade capture. You console yourself thinking about how things might have turned out so much worse.

Draco Malfoy will follow in his father footsteps and go into politics. However, he will shock his family by becoming a supporter of social reform, championing democracy and universal suffrage. You will not regret your decision to save Malfoy's life at the cost of letting Bellatrix escape, though sometimes Malfoy's sharp tongue will make you consider killing him yourself.

You, Harry Potter, will remain a professional duelist and have a long and successful career.
*if (Friendship > 60)
  You will remain close friends with Malfoy for the rest of your life, often meeting up with him for a drink of ${drink} whenever your schedules allow it. Although your background is wildly different, you will discover that you have much more in common than you thought.

  In time you will become aware of your lingering attraction for Malfoy, but you'll never act on in for fear of ruining your friendship. Overall, though, you'll have a pretty good life!

  [b]THE END[/b]
  *ending
*if (Friendship < 40)
  You and Malfoy will remain rivals for the rest of your life. You will try to avoid him, but you will both be at the same parties, he as an esteemed guest, you as the entertainment. In these occasions, you will invariably end up arguing: your backgrounds are too wildly different for you to ever find common ground.

  In time you will become aware of your lingering attraction for Malfoy, which is in equal parts baffling and annoying. You will never act upon it. Overall, though, you'll have a pretty good life!

  [b]THE END[/b]
  *ending
*else
  You and Malfoy will go your separate ways. You will keep in touch for a while, but your strange mix of friendship and rivalry will make it difficult for you to find common ground and you will eventually fade from each other's life. You will sometimes see each other at a social event in which he's an esteemed guest and you're the entertainment. However, you will only nod at each other across the room.

  In time you will become aware of your lingering attraction for Malfoy, but you will dismiss it as a stupid notion and never act on it. Overall, though, you'll have a pretty decent life!

  [b]THE END[/b]
  *ending

*label good_end
*page_break

After a while, Malfoy falls asleep, leaning against you for warmth. You stay awake and on your guard all night: you're still strung up from everything that happened and you can't discard the possibility that Bellatrix will come back.

A couple of hours before daybreak, the cavalry finally shows up. Lady Malfoy has roused the whole of ${parliament} and sent search parties all over. Lady Malfoy herself rode out to the Lestrange villa in elegant carriage, now quite drenched with rain.

There is no public display of affection, because nobles are not supposed to hug each other in public, but you don't miss the look of relief on her face when she sees her son, or the lump in Malfoy's throat as he assures her that he is all right.
*if (Friendship > 50)
  She doesn't comment on the fact that you and Malfoy are still leaning close together.

*page_break

You and Malfoy share a carriage ride back to ${town}, in which you're both silent and stare awkwardly at everything but each other. You're not sure about what just happened, but you think Malfoy doesn't regret it, which is good because you're quite sure that you don't regret it. In fact, you're thinking that you'd like to get him alone again sometime soon, possibly somewhere without his mother and a dozen ${auror}s in attendance.

For now, though, you go your separate ways after crossing the city walls: he continues towards his fancy town house, while you turn south towards ${home}. There is a lot you need to tell Hermione and Ron.

You find out later that Bellatrix Lestrange has been captured while trying to board a ship out of the country. She will later be transported back to ${town} where she will face trial for murder and kidnapping. Being a noblewoman she will probably be spared the death penalty, but you are satisfied with the thought that she will spend the rest of her life in prison.

*page_break Epilogue

*temp mname "Malfoy"
*if (Friendship > 50)
  *set mname "Draco"

Draco Malfoy follows in his father footsteps and goes into politics. However, he shocks his family by becoming a supporter of social reform, championing democracy and universal suffrage.

You suspect Hermione to have played a part in this change of heart, though you're not sure of when she and ${mname} started having intense political debates.

Ron doesn't approve, mostly because Hermione gets so worked up when talking about politics. Ron and ${mname} learn to put aside their differences after a while, or at least they manage to tolerate the sight of each other without coming to blows, which you consider a definite improvement.

*if (Friendship > 50)
  (Yeah, you end up calling him "Draco" after you start sleeping together. It just feels weird to do otherwise.)

*if not(Friendship > 50)
  (Yeah, you still call him "Malfoy" even after you've been sleeping with him for years. It just feels weird to do otherwise.)

You, Harry Potter, remain a professional duelist and have a long and successful career. You make an appearance at Lady Malfoy's birthday party, and then to every single one of ${mname}'s parties, but as a guest rather than in a professional capacity. People might gossip about you and ${mname}, and guess the truth about your relationship, but nobody dares to say anything to your face. Not that you care what strangers say.

${mname}, on the other hand, retains his volatile temper: you lose track of the number of duels you have to fight for him over the years.
*if (Friendship > 50)
  However, you never once regret your decision to save his life.

*if not(Friendship > 50)
  You almost never regret your decision to save his life.

[b]THE END[/b]
*ending

*label true_end
*set UST 100
*page_break

You watch the woman fall down and let out a long breath. Malfoy pushes you aside to check on her. You don't know when he took your ${weapon}.

"She's still alive," he says. "Help me tie her up, quickly!"

His imperious tone makes the situation somewhat familiar, even though Malfoy looks a sight. He's lost his neckcloth and his jacket and his shirt is liberally streaked with grass and mud. You don't look much better, either, and the cut in your arm is bleeding badly and will need the attention of a ${healer} as soon as possible.

The two of you tie up Bellatrix and her henchmen and toss them inside the carriage. Malfoy does most of the work, since you're feeling somewhat woozy. You sit down on the side of the road, trying to think of what to do next. Calling the ${auror}s might be a good start.

"You know, Potter," Malfoy says with a smirk. "This time I was the one who rescued you. You owe me."

You snort. "That doesn't even count," you say. "I was the one who came after you, I'm still definitely the one who did the rescue here."

Then Malfoy has to put his arm around your shoulders to stop you from collapsing because of the blood loss.

*page_break

The ${auror}s show up shortly after. You think that's very convenient and timely, but unknown to you Lady Malfoy roused the whole of ${parliament} and had search parties sent in every direction to look for her son.

Lady Malfoy herself shows up shortly after, riding in an elegant open carriage. There is no public display of affection, because nobles are not supposed to hug each other in public, but you don't miss the look of relief on her face when she sees her son, or the lump in Malfoy's throat as he assures her that he is all right.

Then, Lady Malfoy departs again, to escort her sister back to ${town} to face judgement. She doesn't comment on the fact that Malfoy's arm is still around your shoulders.

*page_break

You don't remember much of the carriage journey back into town. Every bump in the road jolts your wound, and you spend most of the time floating in and out of consciousness.

At some point you pass out, because when you come to you're no longer in the carriage but rather lying on a bed in a room you don't recognize. The bed is large and incredibly soft, and it's surrounded by green curtains that are currently thrown open to let in the warm afternoon sun.

Malfoy is dozing on an armchair near the bed, but he jerks awake when he hears you moving around.

"$!{healer} Pomfrey said you should rest," he says, pushing you back when you try to get up. His hand feels surprisingly gentle on your shoulder. If you didn't know better, you'd say he was concerned for your well-being.

You compromise by wriggling into a sitting position. Your arm, from the elbow to the tip of your fingers, is swathed in bandages. "Where am I?" you ask, carefully prodding at the bandages. "The Hospital?"

*page_break

Malfoy rolls his eyes. "I wouldn't trust the accommodations there," he drawls, and for a moment he's back to his usual, sneering self. "No, I had you taken home and sent for the family ${healer}. It was the least I could do, under the circumstances."

You blink, taken aback. It's a kindness you didn't expect: he's clearly gone to some pains to make you comfortable, and then he sat up with you while you slept, even though he was very tired himself. "Thanks," you say.

But he makes a brusque gesture, clearly uncomfortable about something. "No, that's... I'm the one who should thank you," he stammers. "You risked everything to save me, and I didn't even notice... Didn't even cross my mind to think..."

"What on Earth are you talking about?" you ask, frowning.

"Merlin's sake, Potter!" Malfoy yells. "That's your ${weapon} arm that you almost lost!"

*page_break

"Oh," you say, looking down at the bandages. The thought hadn't even crossed your mind, having been rather more preoccupied with saving Malfoy's life and your own, and later with apprehending Bellatrix. "I didn't lose the arm, though, so I don't see what you're so worried about..."

"What if it doesn't heal properly?" Malfoy snaps. "What if you can't duel ever again?"

You consider this. It fills you with dread, because you're a duelist to the core and you can't think about doing anything else with your life. But if your arm is the price to pay for Malfoy's safety, you'll consider it a good bargain. That's not something you can tell Malfoy, though, so you simply shrug and give him a rueful grin.

Malfoy makes a small, frustrated noise in the back of his throat. "You shouldn't have done it for me," he says, and then he throws his arms around your neck and pulls you close, burying his face in the crook of your neck. "You shouldn't have!"

You return the hug with your good arm, holding him close. "It'll be okay," you whisper. "I promise."

*page_break

*page_break Epilogue

Your arm eventually heals and you're able to keep working as a duelist. You'll always have a scar where Bellatrix's ${weapon2} cut you, but that's not your first scar, so you don't mind. Malfoy blames himself for a long time after you've recovered the use of your arm. The confrontation with his aunt has shaken him badly and left him a changed man: he's much calmer now, and he no longer picks fights with random strangers just to stave off boredom.

He's still got a sharp tongue, and he'll never be precisely [i]nice[/i], but you wouldn't change a thing about him; even though Ron says that it makes you sound sappy when you say that.

Your friends are appalled when you tell them that you are dating Malfoy, but they eventually accept it when they realize that Malfoy cares about you just as much as you do about him. Ron never warms up to Malfoy, precisely, but the two of them manage to tolerate the sight of each other without coming to blows; you consider this a definite improvement.

As for Hermione, she eventually strikes up an odd sort of friendship with Malfoy, made of snark and intense social debates. They often spend the evening talking together about books and politics, while you and Ron stare at them without understanding a word of what they're saying. It makes you feel oddly left out, but as Malfoy points out, after those evening debates he does spend the rest of the night with you, so you've got no right to be jealous.

*page_break In the following years...

Draco Malfoy ends up following in his father footsteps and goes into politics. However, he shocks his family by becoming a supporter of social reform, championing democracy and universal suffrage. You suspect Hermione has played no small part in this change of heart, and you start borrowing her newspaper so you can follow Draco's career.

*if (Home="Knockturn Alley")
  As for your relationship with Draco, society's attitude forbids you from being open about it. Only your close friends and family know the truth. You sometimes fear that Draco's political career would be over if it became known that you are lovers, but even though there are whispers nobody dares to say anything out loud. They're probably afraid that if they did you would call them out. They're right.

*if (Home="Riverside")
  As for your relationship, Draco is very private about it, but it eventually becomes known in the city that the two of you are lovers. Fortunately, society is rather tolerant of this kind of relationship, so Draco's political career isn't harmed. There are some people who say that your low birth makes you an unsuitable paramour for the future Head of the Council, but they don't dare to say so in your or Draco's hearing.

Eventually, your ${weapon} arm starts giving you trouble, and you realize it never really healed properly. You feel surprisingly calm when you make the decision to retire as a professional duelist; by now you've earned enough ${galleon}s to make yourself comfortable, you've been living together with Draco for several years, and you know how much he frets whenever you fight. It's been a long while since the time when he used to challenge people on a daily basis to make you fight for him.

You, Harry Potter, achieve the greatest success that a duelist can get: you retire of your own free will, alive and happy. You never quite give up your ${weapon} though. You open a school in which you train the next generation of duelists, including (to Hermione's chagrin and pride) young Rose Weasley.

You and Draco live, as your friends would say, disgustingly happy ever after.

[b]THE END[/b]
*ending

*finish