*temp huff false

Over the next week you receive the visit of a couple more noblemen wanting to hire you, but you don't commit to any new engagement yet. You still have enough gold ${galleon}s to last you the rest of the month, so for the time being you're enjoying the life of a gentleman of leisure.

One afternoon, Hermione asks you for a favour. She's an avid reader, but books are precious and much too expensive for common folk like you. With little Hugo and Rose to provide for, Hermione hasn't been able to afford a new book in a while. So she borrows them instead, from a dusty bookstore who loans them out for a few ${knuts} a week. Today she's busy helping Ron in the back, so she asks you to return a book and borrow a new one.

You cross the river into the posh side of the city. It's a whole new world compared to ${home}: here the streets are clean and lined with trees, and shopkeepers display their wares outside with no fear of thieves. You don't often come here, unless you're passing through on your way to an assignment, so you wander around aimlessly for a while enjoying the unfamiliar sights and sounds and smells.

*page_break

Eventually, your feet bring you to Hermione's bookstore. You've been here with her once before, and anyway it's not difficult to find, situated as it is right in the middle of ${mainstreet}.

The bell jingles merrily as you step through the door. You look around at all the shelves filled with books of all kinds: you'll never be able to find the one that Hermione wanted among all those others. You don't see anyone, not even the owner, but finally you hear a rustling of pages in a faraway corner.

"Excuse me, I'm looking for a book..." you begin, stepping behind a shelf full of leather-bound volumes.

There's a person sitting in an armchair in a corner, but it's not the owner of the store. It's Draco Malfoy, and at the sound of your voice he looks up from the book he's reading and frowns at you.

*if (UST = 30)
  Malfoy is dressed even more richly today, in a dark green coat and breeches, and he has a pair of silver-rimmed rectangular glasses perched on his nose.

*if (UST < 30)
  Malfoy is dressed even more richly today, in a dark green coat and breeches, and he has a pair of silver-rimmed rectangular glasses perched on his nose. Aside from a fading bruise and a couple of scratches on the side of his face, he seems completely recovered from his fight.

*page_break

*if (Friendship < 50)
  "Looking for a book? In a bookstore?" Malfoy snarls. "I'm not sure your mental powers will prove up to the task." He gazes pointedly at the hundreds of books all around you.

  "Very funny," you spit back. You turn around and leave him be: it's clear that your curt refusal of his request to fight the stupid birthday duel has hurt his stupid pride.

  Soon after, you hear the bell jingle again. Malfoy has left.
  *set huff true
  *goto bookstore_shopkeeper

"Looking for a book? In a bookstore?" Malfoy drawls. "That sounds rather difficult, are you sure you'll be up to the task?" He gazes pointedly at the hundreds of books all around you.

"Very funny," you reply, rolling your eyes. "I'm looking for a specific book, I thought you were the owner."

"I'm not," he says with a casual shrug of his shoulders. Then he narrows his eyes. "Are you trying to insinuate I look like a tradesperson?"

For a nobleman such as Malfoy, being in trade is a capital sin. Their wealth comes from their large estates, but even those gentlemen with a meager income would rather starve than work for a living. You weren't trying to pick a quarrel with Malfoy, though: it's clear that your refusal of his request to fight the birthday duel hurt his pride, so he's acting all high and mighty.

*page_break

You ignore him and look at the books on the shelves. Maybe, by some stroke of luck, you'll happen on the book you're looking for.

After a couple of minutes of fruitless searching, Malfoy speaks again. "What's the title of this book, anyway? Maybe I can find it for you."

You turn around, surprised by that. He's pretending to read and won't look at you. It's a kind offer, but Malfoy doesn't strike you as a particularly kind person so you wonder what's his angle.

*choice
  #Accept his offer
    *set Friendship %+20
    *goto bookstore_malfoy
  #Refuse and wait for the owner
    *set Friendship %-10
    *set huff true
    "No, thank you, [i]my lord[/i]," you say in a mock deferential tone. "I wouldn't want anyone to walk in and mistake you for a shopkeeper."

    He glares at you. "Suit yourself," he says, closing his book with a snap. He leaves the store in a huff, slamming the door behind him.
    *goto bookstore_shopkeeper

*label bookstore_shopkeeper

*page_break

*if (bellatrix_letter)
  The owner of the bookstore shows up shortly after. You return the book Hermione has given you and show him the title of the book you're looking for, which Hermione wrote down for you on a piece of paper.

*if not(bellatrix_letter)
  The owner of the bookstore shows up shortly after. You return the book Hermione has given you and carefully recite the title of the book you're looking for, which she made you learn by heart since you had no paper on which to take notes.

The elderly storekeeper presents you with the large, leather-bound volume that Hermione requested. You groan, wishing Hermione would rather read one of the slim pocket-sized books instead, but she's never been one for light reading. You sign the bookstore's lending registry, hand over a few ${knuts}, and take your leave.

You step outside with the heavy book under your arm. It's a fine day, despite the snow covering the streets and being trampled into mud by horses and carriages.

*goto bookstore_outside

*label bookstore_malfoy

*if not(bellatrix_letter)
  "Alright," you say. There's no call to be rude to Malfoy just because he's strangely decided to be helpful. You carefully recite the title of the book you're looking for, which Hermione made you learn by heart since you had no paper on which to take notes.

  Malfoy frowns taps his long fingers on the book he's reading. He's got gentleman's hands, soft and unmarred, unlike your own which are calloused from drilling your ${weapon}smanship every day.

  "Yes, I know that book," he says. He strides away towards another part of the shop and starts pulling books from a shelf.

  You follow him, noting the ease and familiarity with which he moves. He might not work here, but he's surely familiar with the layout of the books.
  *goto bookstore_explains

"Alright," you say. There's no call to be rude to Malfoy just because he's strangely decided to be helpful. You show him the title of the book you're looking for, which Hermione wrote down for you on the back of an envelope because it was the only piece of paper you had.

*set malfoy_saw true
Malfoy's eyes go wide and he snatches the envelope from your hand. "Who gave this to you?" he asks. You don't understand what he means, so he clarifies. "The letter, Potter! Is this from one of your clients?"

"Oh, that," you say. You shrug. "I have no idea who sent it and I'm not planning to do business with them anyway. I only talk to clients in person."

*page_break

You watch Malfoy with curiosity, wondering if he knows the mysterious sender of the letter with the black seal, but his face doesn't betray any emotion. He taps his long fingers on the envelope. He's got gentleman's hands, soft and unmarred, unlike your own which are calloused from drilling your ${weapon}smanship every day.

"Yes, I know that book," he says, handing the envelope back to you. He strides away towards another part of the shop and starts pulling books from a shelf.

You follow him, noting the ease and familiarity with which he moves. He might not work here, but he's surely familiar with the layout of the books.
*goto bookstore_explains

*label bookstore_explains

*page_break

"Here it is," Malfoy says, pulling a large, leather-bound volume from its shelf. He leafs through it. "A history of ${parliament} and of its founding members. Not precisely light reading," he says, giving you an inquisitive look.

"It's for a friend," you explain.

His face clears up. "Yes, you didn't strike me as the reading type," he says.

You fight the urge to blush. It's true: you've only learned your letters so you can read and write your name, which is more than most people from ${home} can do. Malfoy, with his highbrow ways and fancy reading glasses, has no right to judge you.

Fortunately, Malfoy changes the subject. "It's quite a dreary book, good luck to your friend. I had to read a few chapters for one of my classes and I kept falling asleep on the pages."

*label convo_choices
*choice
  *hide_reuse #Ask him if he's a student
    *set Friendship %+10
    "Are you a student up at the University?" you ask.

    "I was," he replies. "I studied ancient literature. But I quit a few months ago."

    "Why?" you ask. Most of the scions of noble families study at the University, it's a prestigious institution dating back centuries. You see some of the students on the streets sometimes, wearing black robes and carrying themselves with a self-important air.

    Malfoy smirks, as if knowing what you're thinking about. "The black clothes didn't suit me," he says. "Besides, the lectures were deadly boring."

    You have the feeling that he's not being entirely truthful, but it was a personal question and you're essentially strangers.
    *goto convo_choices
  *hide_reuse #Ask him why he's so familiar with this store
    *set Friendship %+10
    "Do you come here often?" you ask. "You seem very familiar with where all the books are."

    He shrugs. "I was accompanying my mother on her morning calls, but there's only so much mindless chatter and watery tea that I can stomach in a day." Morning calls are when ladies and gentlemen pay short visits to their friends and acquaintances, and among the fashionable set they always take place in the afternoon despite the name; you always thought that the custom sounded silly and you smile at Malfoy's look of exaggerated boredom.

    "I like this bookstore," Malfoy continues. "It's quiet, and the owner doesn't mind if I sit and read for hours at a time. At home, I keep being interrupted by people who come calling."
    *goto convo_choices
  #Ask him why he's suddenly being so helpful
    *set Friendship %-20
    *set huff true
    "Why did you help me find this book, anyway?" you ask, frowning. "If you thought I'd change my mind about the duel, that's not going to happen."

    "That's not it," Malfoy says, curtly.

    "So what do you want? I know what noblemen are like, you always want something in exchange."

    He pulls a face. "Maybe I thought it was the least I could do, since you stopped my duel the other day. I should have figured that manners would be wasted on you. Good day, Potter!"

    He shoves the book in your hands with bad grace and leaves, slamming the door behind him.
    *page_break
    *goto bookstore_pay
  #End the conversation
    You thank Malfoy for finding the book and say your goodbyes. He leaves, jingling the bell on the door.
    *goto bookstore_pay

*label bookstore_pay

The owner of the bookstore shows up shortly after. You return the book Hermione had borrowed and show him the other book that Malfoy found for you.

The elderly storekeeper makes you sign the lending registry and you hand over a few ${knuts} to pay the borrowing fee. Then you take your leave.

You step outside with the heavy book under your arm. It's a fine day, despite the snow covering the streets and being trampled into mud by horses and carriages.

*label bookstore_outside

*page_break

You decide to take the scenic route, walking home along the river. However, you haven't gone far before you run into Malfoy again. He's standing at the corner of the street, talking with two friends. You recognize one of them as Blaise, Lord Zabini's son, and the other is the Honourable Miss Pansy Parkinson who recently got engaged to him.

*if (huff)
  Malfoy ignores you as you walk past the small group of nobles, turning around and pretending he doesn't know you. The snub irks you, so you tip your hat at the lady and say "Good day to you too, Malfoy" in a loud voice. You keep walking.

*if not(huff)
  Malfoy nods to you as you walk past the small group of nobles. You return his nod and, after a moment's hesitation, you tip your hat at the lady. You keep walking.

You hear a shocked gasp.

"Draco," Miss Parkinson says. "Who was that grubby little man? Do you [i]know[/i] him?"

*if (huff)
  "Of course not!" Malfoy exclaims, sounding completely outraged at the insinuation that he's associated with a commoner. "I've never seen him before."

*if not(huff)
  "Just the merest acquaintance," Malfoy mutters, uncomfortable at the idea of being associated with a commoner. "Don't mind him, Pansy."

His friends, however, seem reluctant to let it go, and you hear the sound of laughter behind you.

*page_break

"I think you do know him," Parkinson says. "I think it's true what the gossips are saying, that you go to ${home} and take lowborn lovers to your bed. Wait until they find out that you prefer male lovers," she adds, with a shriek of laughter.

"Whores, more like," Blaise Zabini says. "Hey, you with the black hair and the book!" he calls out to you. You stop and turn around, because despite yourself you're curious to see how this will play out. "Tell me, how much did Draco pay you to stick it into you? Or does he prefer it the other way around?"

You blush at the words and at the vivid mental picture that they conjure. You wouldn't have thought Malfoy to be attracted to members of his sex, and likely it's only something Zabini is saying to embarrass him. But now that it's been suggested, you find it hard to put it from your mind.

Meanwhile, Miss Parkinson makes a show of scolding her friend for using vulgar language, but she's giggling behind her hand.

Malfoy is furious. "You go too far, Blaise!" he snarls, drawing his ${weapon}.

*page_break

Zabini and Parkinson seem to find this terribly amusing. "Are you challenging me?" he says, in between bursts of laughter. "Be careful not to ruin your manicure."

Parkinson howls with laughter.

"There's no risk of me harmed since you're an incompetent fool," Malfoy snarls. "You too, Pansy! I challenge you both, if you have the guts to fight me. I'll fight you both together to save time, since I'm expected at my aunt's for dinner."

This boast stops the laughter, though Zabini and Parkinson are still smirking as they draw their ${weapon}s.

"That's fine with me," Zabini says, as Parkinson starts circling Malfoy.

Most members of the nobility prefer to wait after issuing a challenge, so both parties have time to find a professional duelist who will fight in their place, but it seems that those three have no such qualms. They're about to fight each other in the middle of the street.

*choice
  #Step in and claim the duel
    *if (UST = 30)
      Despite Malfoy's claims about his opponents' lack of skills, it can't be easy for him to fight two people at the same time.

    *if (UST < 30)
      Despite Malfoy's claims about his opponents' lack of skills, it can't be easy for him to fight two people at the same time. Besides, you've already seen that he's no great duelist himself.

    You drop the book you're carrying and step forward. "Wait. I'm going to claim this duel. I'll fight you in Malfoy's place."

    Malfoy doesn't seem surprised by this, and he calmly re-sheathes his ${weapon}. It's as if he knew he could safely challenge his two friends because you'd come to his rescue. Knowing him, that's exactly what happened. Cheeky bastard.
    *set UST +30

    *page_break

    Meanwhile, Parkinson and Zabini exchange a look. "Is Malfoy that good of a customer that you feel the need to defend him?" Zabini asks.

    "He's not my customer," you say, glaring at Malfoy who's looking quite pleased by this turn of events. "Although he did offer to hire me, but not for the purpose you assumed. I'm Harry Potter, professional duelist." You sketch an elaborate bow and grin as you see the growing horror and dismay on their faces. "You might have heard of me," you add.

    "It's true, that's the duelist," says a woman. The argument has attracted the attention of several passers-by, who are all crowding around you to see what's going on. "I saw him last summer at Baroness Chang's masquerade ball!"

    Usually you don't like being recognized, but sometimes it comes in handy. Parkinson and Zabini are already stepping back. However, the noblemen's code stops them from running away from a duel.

    *page_break

    Parkinson is the first to get her wits about her. "If Draco is having someone else fight for him, then we can have proxies as well," she says. Then, raising her voice to carry across the street, "A hundred ${galleon}s to whoever claims this duel for me!"

    "Two hundred ${galleon}s," Zabini chimes in.

    They're sweating profusely, afraid that your name will keep anyone from taking their offer, but finally two men come forward through the crowd. They're both big, brawny types, and their ${weapon}s look like toys in their huge hands. Their coats are of a cut that was fashionable several seasons ago. Younger sons, you think, or scions from an impoverished family who have to make a living by dueling.

    "We'll take that offer, Miss," says one of the fighters. "For two hundred ${galleon}s each."

    *page_break

    A curt nod from Parkinson seals the deal.

    You give a slight bow to your opponents, who don't even incline their heads. They don't look like they care a bit for the niceties. That's fine by you, you also would rather end this quickly.

    The man on your right starts it, coming at you with a strangled yell. He's big and slow, though, and you easily parry his ${weapon2}. The other man tries to sneak around you and take you from behind, but you've often had to fend off three or even four opponents in alley brawls in ${home} while growing up, and it takes a lot more skill to get the jump on you.

    *if Weapon="sword"
      You sidestep your opponents and pass to the counterattack, lounging at the nearest duelist with a vicious slash that hits him in the upper arm. He stumbles back, holding his bloodied and torn sleeve against the cut, and you use the distraction to sweep his friend's legs from under him, sending him sprawling into the mud.

    *if Weapon="wand"
      You sidestep your opponents and pass to the counterattack, aiming at the nearest duelist with a vicious stinging hex that hits him in the upper arm. He stumbles back, holding his bloodied and torn sleeve against the cut, and you use the distraction to sweep his friend's legs from under him, sending him sprawling into the mud.

    *page_break

    The crowd of onlookers bursts into a cheer. You glance at Malfoy, who's lounging against a lamppost and clapping idly.

    "Do you feel that your honour has been sufficiently satisfied, Malfoy?" you ask with mock-deference.

    He nods. "I think so. What do you say, Pansy? Blaise?"

    Parkinson and Zabini exchange a look. "Yes, I feel that's enough," Parkinson says. "Given the outcome of this duel, we concede that we were in the wrong. Come now, Blaise."

    They leave in a hurry, after arranging for the hapless duelists' payment. The two of them look dazed and they're all covered with mud and bruises. Still, that's a good bargain for all those ${galleon}s.

    Finally, having been satisfied that no more interesting duels will happen here, the crowd disperses.
    *goto bookstore_end
  #Stay and watch the show
    *goto slyth_duel
  #Walk away
    You try to leave unobtrusively, but the loud argument has already attracted the attention of several passers-by. Now they're crowding around to see what's going on, and blocking the sidewalk.
    *goto slyth_duel

*label slyth_duel

The three nobles bow stiffly and face each other. They're all in some laughable beginner's stance that some training master might have taught them in the schoolroom.

Malfoy and Zabini go at each other first, while Parkinson seems content to stand on the sidelines and watch. It's obvious from the first clash of ${weapon2} against ${weapon2} that none of them is a great dueling prodigy. They're both on the defensive and use fairly standard moves that are easy to parry. Clearly, despite being hot-headed enough to fight each other, they don't want to be injured and so they're on the defensive.

You're starting to think that Malfoy will be all right on his own, but then Parkinson steps in. She raises her ${weapon} and lunges at Malfoy from the right, and he has to move quickly to avoid being skewered by her ${weapon2}. That however makes him vulnerable to Zabini's attack.

You thought Parkinson was holding back because being a lady she didn't want to duel, but you see now that she was only studying her opponent. Out of the three she's actually the best duelist, going after Malfoy's weak spots with ruthless determination.

You wonder if after all you ought to intervene.

*temp opponent
*temp opponent_him
*temp opponent_his

*choice
  #Step in and fight both opponents
    *set UST +20
    *goto slyth_2
  #Step in but fight only Parkinson
    *set UST +10
    *set opponent "Parkinson"
    *set opponent_him "her"
    *set opponent_his "her"
    *goto slyth_1
  #Step in but fight only Zabini
    *set UST +10
    *set opponent "Zabini"
    *set opponent_him "him"
    *set opponent_his "his"
    *goto slyth_1
  #Do nothing
    You felt responsible when Malfoy was threatened in ${home}, but here it's no problem of yours who he quarrels with. Besides, you don't think Malfoy's life is in any great danger. Even noblemen draw the line at killing each other in the streets in broad daylight.

    *if Weapon="sword"
      Suddenly, Parkinson lunges forward. Malfoy ducks but not quickly enough: her blade hits him in the face, leaving him bleeding and howling in pain.

    *if Weapon="wand"
      Suddenly, Parkinson raises her wand and casts Sectumsempra. Malfoy ducks but not quickly enough: the curse hits him in the face, leaving him bleeding and howling in pain.

    Smug from their victory, Parkinson and Zabini help their injured friend into a carriage and leave.

    The whole thing is over in a flash. Having been satisfied that no more interesting duels will happen here, the crowd disperses, but you stand there for a long while looking at the blood marring the sidewalk.

    *page_break

    That's the last time you see Draco Malfoy. There is a lot of talk about this duel on both sides of the river, but the general consensus is that his injuries are not life-threatening. You go back to the bookstore a few times but he's never around. Eventually you hear that Malfoy's face was permanently scarred by Parkinson's ${weapon2}. Being ashamed of showing himself in public, he retreated to the family estate in the country where he's living the life of a hermit, never leaving his rooms.

    You, Harry Potter, will continue your career as a duelist. However, you will not be as sought-after as you used to be. A few people saw you in the crowd during Malfoy's duel, and they will spread the rumour that you abandon your clients and leave them to fend off for themselves. That's a lie, because you had no obligation to Malfoy, but people believe what they will. With less patrons, you're no longer able to pick and choose, and each new job makes you feel more bitter and resentful towards those nobles who think nothing about ruining each other's life.

    [b]GAME OVER[/b]
    *ending

*label slyth_2

You drop the book you're carrying and step forward. "I claim this duel," you say, loud enough for everyone around to hear you. "I'll fight you both in Malfoy's place."

Malfoy lowers his weapon. "Took you long enough," he mutters, breathing hard from exertion.

"Fine by me," Parkinson says. She's barely breaking a sweat.

They both come at you at the same time, and you've barely got time to draw your ${weapon} before you're assaulted by a flurry of ${weapon2}s.

*page_break

Parkinson is good, but she's still an amateur. As for Zabini, he's just as hopeless as Malfoy said.

*if Weapon="sword"
  You sidestep your opponents and pass to the counterattack, sweeping Zabini's legs from under him and sending him sprawling into the mud. Then you parry Parkinson's thrust and, locking her blade with your own, you knock her sword out of her hand and send it flying.

*if Weapon="wand"
  You sidestep your opponents and pass to the counterattack, sweeping Zabini's legs from under him and sending him sprawling into the mud. Then you parry Parkinson's jinx and, with a well-timed Expelliarmus, you knock her wand out of her hand and send it flying.

The crowd of onlookers bursts into a cheer. Even Malfoy, who's lounging against a lamppost, is clapping idly.

"That's Potter, the duelist!" exclaims a woman. "I saw him use that same move last summer at Baroness Chang's masquerade ball!"

*page_break

Usually you don't like being recognized, but sometimes it comes in handy. Parkinson freezes, and Zabini scrambles backwards in the mud.

"I yield!" Zabini exclaims.

Parkinson looks wistfully at her ${weapon}, lying on the ground several feet away, then pulls a face. "Given the outcome of this duel, we concede that we were in the wrong. Come now, Blaise."

"Yes, I think honour is satisfied," Malfoy agrees.

Parkinson and Zabini leave in a hurry, looking all the worse for wear covered as they are with mud and dust. Finally, having been satisfied that no more interesting duels will happen here, the crowd disperses.

*goto bookstore_end

*label slyth_1

*if opponent="Parkinson"
  Parkinson is the biggest threat for Malfoy. You figure that, if you take care of her, he'll be able to manage Zabini on his own.

*if opponent="Zabini"
  You don't like the idea of dueling against a lady. You figure that, if you take care of Zabini, Malfoy will be able to manage Parkinson on his own.

You could easily fight them both blindfolded and with a hand tied behind your back, but you don't want to make things too easy for Malfoy. The cheeky bastard probably knew that he could challenge them with impunity because you'd come to his rescue.

You drop the book you're carrying and step forward. "I claim the duel against ${opponent}," you say, loud enough for everyone around to hear you. "I'll fight ${opponent_him} in Malfoy's place."

Malfoy glances at you. "Took you long enough," he mutters, breathing hard from exhertion.

"Fine by me," Parkinson says. She's barely breaking a sweat.

*page_break

*if (opponent="Parkinson") and (weapon="sword")
  Parkinson is good, but she's still an amateur. You parry her thrust and pass to the counterattack. Locking her blade with your own, you knock her sword out of her hand and send it flying. The whole thing takes you no longer than a minute.

*if (opponent="Parkinson") and (weapon="wand")
  Parkinson is good, but she's still an amateur. You parry her jinx and pass to the counterattack. With a well-timed Expelliarmus, you knock her wand out of her hand and send it flying. The whole thing takes you no longer than a minute.

*if opponent="Zabini"
  Zabini is just as hopeless as Malfoy said. You don't even need to use any of your signature moves against him. Sidestepping him, you sweep Zabini's legs from under him and send him sprawling into the mud with ignominy. The whole thing takes you no longer than a minute.

The crowd of onlookers bursts into a cheer.

"That's Potter, the duelist!" exclaims a woman. "I saw him use that same move last summer at Baroness Chang's masquerade ball!"

Usually you don't like being recognized, but sometimes it comes in handy. Malfoy is still fighting, but at those words both ${opponent} and ${opponent_his} friend freeze and turn towards the woman.

Malfoy takes advantage of the distraction to gain the advantage, cornering his opponent.

*page_break

"I yield!" Zabini exclaims, and Parkinson follows suit, albeit more reluctant to abandon the fight.

"Given the outcome of this duel," Parkinson says, "we concede that we were in the wrong. Come now, Blaise."

"Yes, I think honour is satisfied," Malfoy agrees.

Parkinson and Zabini leave in a hurry, looking all the worse for wear covered as they are with sweat and dust. Finally, having been satisfied that no further duels will take place here, the crowd disperses.

*label bookstore_end

*page_break

You retrieve the book that you'd dropped on the sidewalk earlier. Its cover is a bit muddied, but it's still whole and readable. You hope Hermione won't give you too much grief for it.

Malfoy approaches you. "I expect you'll want to be paid for your services," he drawls, reaching for his pocketbook.

You pull a face. "Then you expect wrong. I only stepped in to stop you from getting killed in a stupid way."

"I was in no danger whatsoever!" Malfoy exclaims. He's lying: he knows he was playing a dangerous game, and you think he enjoys the risk. You have no idea if it's boredom or stupidity that makes him do the things he does.

"Yeah, well, you're welcome," you say. Usually after a duel you feel tired but happy about a job well done, yet this fight against amateurs gave you no satisfaction.

*page_break

*if (Friendship > 50)
  Malfoy pouts. "Thanks, Potter," he mutters. He sounds awkward but sincere. "I owe you. Any chance you'll fight that other duel for me too?"

*if not(Friendship > 50)
  Malfoy glares at you. "Thank you so much, Potter," he says. His voice is dripping with sarcasm. "Any chance you'll fight that other duel for me too?"

"None, sorry," you reply in much the same tone.

You go your separate ways: him uphill to his fancy town house, you across the river to your garret room.

The two of you are from completely different words, but something tells you that you haven't seen the last of Draco Malfoy yet.

*finish Winter turns into early spring...