Despite the late hour, the ${pub} is still open, and doing a brisk trade judging from the sounds of drunken merriment coming from inside. It's an old building, like all others in this part of town, but it's well-kept, with whitewashed walls and wooden floors polished by generations of feet.

You take the back stairs up to your rooms and you meet Hermione on the landing. Hermione and her husband Ron own the ${pub}, and they've often forgone your rent when you were still poor and unknown to the nobility of ${Town}. Nowadays you always make sure to pay on time, and to tip well.

You take out a few ${galleon}s for yourself and you give Hermione the purse with the pay from tonight's job. She glares at your ${weapon}, still in its scabbard, but she takes the money. Hermione doesn't approve of your chosen profession (or of noblemen in general) but she knows you're making a living the only way you know how.

*page_break

You give Hermione a rueful grin. "At least the nobs pay well," you say.

"They can afford it," she replies, pocketing the purse. It's just the price of an evening's entertainment to Mr Zabini, but it's a month's rent for Hermione and Ron and their two little children.

You shrug. You're too sleepy to talk about the injustices of society with Hermione, she's capable of going on and on until dawn. It's very late.

*choice
  #Go to bed and get some sleep, I'm knackered
    Hermione stops you as you're walking away. "I think Ron was looking for you earlier. He said he had a message for you."
    *choice
      #Go downstairs and talk to Ron
        You bid Hermione goodnight and head to the tavern downstairs. Ron is behind the counter, filling glasses and taking orders from regulars. The ${pub} is packed, but everyone moves aside to make space for you as you walk in. A reputation is a useful thing to have.
        
        Ron sets down a ${butterbeer} in front of you before you can say anything. The first sip warms you all over. "Thanks, I needed that," you say once Ron has a spare minute between one customer and the other. "Hermione said you were looking for me?"
        
        "Oh, that," Ron says, looking sheepish.
        *goto theletter
      #Ron can wait until tomorrow
        *goto nofun
  #It's not that late, I'll have a drink with Ron at the ${pub}
    You bid Hermione goodnight and head to the tavern downstairs. Ron is behind the counter, filling glasses and taking orders from regulars. The ${pub} is packed, but everyone moves aside to make space for you as you walk in. A reputation is a useful thing to have.
    
    Ron sets down a ${butterbeer} in front of you before you can say anything. The first sip warms you all over. "Thanks, I needed that," you say once Ron has a spare minute between one customer and the other. "It's freezing outside."
    
    "Hey, I've got a message for you," Ron says, looking sheepish.
    *goto theletter

*label theletter

He retrieves a rather large sealed envelope from his vest pocket, and smooths out the creases before handing it to you. "Now before you say anything, I told her that you only do business in person and that she should wait and speak to you, but she was very insistent."

*page_break You take the letter...

The letter is written on thick, expensive ${paper}. It's sealed with a blob of black wax stamped with a noble crest that you don't recognize. Even without opening it, you know what's written in it: some rich noblewoman wants to hire you because some other noblemen's pet poodle has stepped on the tail of her persian cat, and her honour demands satisfaction. Or some such nonsense. Now she expects that you'll come running just because she can pay your fee.

Even when you were poor, you were never so desperate that you took a commission without meeting in person with the man or woman who was hiring you. Your patrons rely on your discretion, and you rely on your patrons to vouch that your victims were killed in fair duel over questions of honour. If you killed someone for a patron and for some reason that patron later denied all association with you, you would be prosecuted for murder.

Even among the noblemen, very few of them would act in such a base and cowardly manner. They're all as proud of their hired duelists' victories as if they were their own, and they only refuse to meet you in person because they feel it's a waste of their time.

Still, those are the rules you've set for yourself, and you're going to stick to them. You're not going to do business by letter.

*choice
  #Pocket the letter
    You're not going to read it, but it's such nice ${paper} it feels a waste to throw it away. You can use it to write notes or something.
    *set bellatrix_letter true
    *goto tavern1
  #Burn the letter
    Into the fire it goes.
    
    "Waste of good ${paper}," says Ron. "But at least the lady tipped me well for delivering it."
    *goto tavern1

*label tavern1

The door of the ${pub} slams open, bringing a gust of cold wind and the smell of snow into the room. The newcomer is a young man, around your age you'd guess. He's tall and thin, and when he pushes down the hood of his cloak you see he's got pale blonde hair. Most of his face is hidden by an elaborate black-and-gold mask.

You don't know who he is, but you know why he's here: he's one of the rich and idle noblemen who venture into ${home} to try and hire your services. You don't know why they bother to disguise themselves. Nobody in ${home} cares about whether someone is the brother of an earl or the wife of a general, as long as they don't cause trouble for the common folk. And still they all wear masks and act furtively, which of course attracts even more attention.

*page_break

Everyone in the ${pub} is looking at the stranger as he strides towards Ron. "Hey, you, barkeep," the stranger says to Ron. He slaps a couple of ${sickle}s on the counter. "I'm looking for the duelist, Potter."

Ron frowns. Even for a nobleman, this one has no manners. "Are you, mate?" Ron says, deliberately insolent, swiping the silver coins off the counter. "Good for you. Can I get you anything?"

The stranger wrinkles his nose and makes a show of looking around the ${pub} with disdain. "No, I wouldn't want to catch anything by actually eating here," he says, smirking. "I'm only paying for information on the duelist."

"I don't sell information," Ron says. He picks up a rag and starts cleaning up glasses. "Like you said I'm a barkeep, I only sell food or drink."

You can see that the stranger isn't used to being refused anything. He makes an angry gesture, and you snicker into your ${butterbeer}. "I was told that the duelist could be found here, so tell me where he is or..."

*page_break

Chairs are pushed back as several regulars get to their feet. 

"Or what?" someone says. You recognize the speaker as Zacharias Smith, an annoyingly opinionated guy and a mediocre duelist. "You nobs don't own us. We might be poorer than you but we're not your servants." He steps in front of the nobleman and with a backhanded slash knocks the mask off his face. It clatters to the floor. "And we don't hide our faces like children playing at masquerades!"

His friends nod and mutter agreement. It's true enough, the people of ${Home} rule themselves because even the ${auror}s are too scared to venture among thieves and cutthroats. A man has to be really arrogant, or stupid, to think he can walk in here and act like he owns the place.

There are several calls of "Bloody nobles" and "Sod off!" from the patrons.

This stranger however is both arrogant and stupid, because instead of backing down he draws his ${weapon}. "I demand satisfaction for that!" he exclaims, his voice high-pitched and angry. From the way he holds the ${weapon} you know he's never fought a duel outside of a lesson room.

*choice
  #Reveal that you're Harry Potter and stop the fight before it starts
    *set UST +30
    Before things can go any further, you get to your feet and step between Zacharias and the stranger. "Break it off," you say in a quiet voice. "No need to fight, I'm standing right here. I'm the one he's looking for." You stare straight at the stranger as you say that.
    
    He scoffs at first, looking at you up and down. "You're the duelist?" he says, disbelief evident in his face.
    
    You fight the urge to blush under the scrutiny. You know you're not much to look at -- short and with unruly black hair and so very common-looking. That's why you don't like to mingle with the nobs more than strictly necessary. You show up, do your job, take your money, and leave. Even if this blond stranger has seen you duel in the past, he'd have seen you at a distance and for a few minutes only. You doubt he'll recognize you.
    
    However, the nobleman nods to you as if satisfied. "Yes, I remember the appalling glasses," he says.

    *page_break

    "Someone ought to teach this prick a lesson," Zacharias mutters. You can smell the stink of booze coming from him.

    "Walk it off," you tell him. "No fighting in Ron's ${pub}."

    Zacharias and his cronies cast you dark glances, but they know better than to try and challenge you. They go back to their seat, and you turn towards an empty table in the corner.

    He puts away his ${weapon} and you sit down in front of each other.
    *page_break 
    *goto tavern_end
  #The stranger deserves a lesson, watch and see what happens
    *goto tavern2

*label tavern2

Zacharias laughs and draws his own ${weapon}. You can smell the stink of booze coming from him. "Do you even know which way to hold a ${weapon}, Lord Fancy Pants?" he says. This taunt gets a loud cheer from his cronies, no less inebriated than him.

The nobleman doesn't look worried in the least. "Even if I didn't know how to hold mine, you're so drunk you're liable to trip and fall on your own," he says. You chuckle despite yourself. He can talk a good game, you'll grant him that.

Tables are pushed away from the center of the room to make space. "I've told you a thousand times already, no fighting in my ${pub}!" Ron says, but he's already storing the most valuable bottles of booze safely under the counter.

"Ten to one there's no fight," says Lee Jordan, loudly enough for the whole room to hear. "The kid'll piss himself as soon as he realizes there's nobody to fight the duel for him..."

*page_break 

*if Weapon="sword"
    With a wordless cry, the stranger lunges forward. Zacharias barely has time to raise his sword and avoid being skewered. The other boy stumbles back, taken by surprise and shocked that the nobleman is putting up any fight at all. "You bastard," Zacharias splutters, while some in the crowd jeer at their friend's discomfiture.

    However, once Zacharias starts fighting for real, you know there is only one way this duel can end. The nobleman is so unused to dueling with a real opponent that he's signaling his every move. Even a lousy duelist such as Zacharias can see in advance where the nobleman is about to move and where his blade is about to fall.

    The stranger is pressed back towards the wall. His eyes dart around, as if he'd like nothing better than to turn and run, but the crowd is pressing around him. He drops his guard for a moment, and Zacharias raises his sword and slashes at his face.

*if Weapon="wand"
    With a wordless cry, the stranger lunges forward, casting curses at Zacharias. The other boy stumbles back, taken by surprise and shocked that the nobleman is putting up any fight at all. "You bastard," Zacharias splutters, while some in the crowd jeer at their friend's discomfiture.

    However, once Zacharias starts fighting for real, you know there is only one way this duel can end. The nobleman is so unused to dueling with a real opponent that he's signaling his every move. Even a lousy duelist such as Zacharias can see in advance where the nobleman is about to move and which spells he's about to cast.

    The stranger is pressed back towards the wall. His eyes dart around, as if he'd like nothing better than to turn and run, but the crowd is pressing around him. Zacharias raises his wand and sends blades of wind hurtling towards his opponent.

*page_break

The nobleman ducks just in time, and instead of a fatal wound receives only a long scratch along his cheekbone. The blood shines dark red against his pale complexion.

You realize with sudden clarity that Zacharias is going to kill this stranger. It would not be the first time that a nobleman has vanished in this part of town. Hell, if it comes to that, this stranger is the one who challenged Zacharias and who attacked first. With so many witnesses, even the ${auror}s would conclude that this is not a murder but a proper duel.

The early bravado has gone from the stranger's eyes. He looks around like a cornered animal. Zacharias is just toying with him now before he delivers the final blow.

*choice
  #Stop this, I've seen enough death already
    *set UST +20
    You kill people for a living, but these people are professional duelists who know the risks. You are also fully aware of the fact that you're putting your own life on the line with every job you accept. But this idiot nobleman had no idea what he was getting into.

    *goto breakoff_pub
  #Do nothing, I couldn't care less
    You kill people for a living. What concern is it of yours if people kill each other for stupid reasons? If a man unsheathes a ${weapon}, he's got to be ready to face the consequences. This idiot nobleman should have known what he was getting into.

    It's over quickly enough. The stranger makes one last desperate attack, but in doing so he leaves his side open, and Zacharias goes in for the kill. Zacharias's ${weapon2} hits true and the stranger crumples to the floor.

    Professionally, you consider that a quick death is a mercy for a duelist. You also think that Zacharias is too slow on his feet, and if it had been you facing him, the duel would have gone very differently. But you chose not to interfere in this duel, and what happened, happened.

    *page_break

    The body of the mysterious stranger will be dumped in the river overnight. It will be discovered that he's the son of a very powerful family and there will be an investigation into the circumstances of his death, but the ${auror}s won't be able to find out anything concrete.

    You, Harry Potter, will have a long career as a duelist. However you will never marry, or have any long-term relationship. The people you date will usually say the same thing: that you care about duels more than you do about people. They're probably right.

    [b]GAME OVER[/b]
    *ending

*label nofun

Whatever Ron has to say, you're sure it'll keep until tomorrow. Duels, even short ones like today's, leave you bone tired.

Your room is cold, since the fireplace hasn't been lit all day. You kick off your boots but decide it's too cold to undress for bed. Still wearing all your clothes, you snuggle under the thin blanket and close your eyes.

Soon enough you're sleeping.

*page_break

The sound of raised voices wakes you up. You open one bleary eye, feeling as if you've been asleep for barely a minute. That might actually be true. It's still dark outside, and it's still snowing.

More shouts. They seem to be coming from the ${pub} downstairs. Curious, you sit up and frown in concentration. You can hear cheering, and then the unmistakeable sound of a ${weapon2} hitting another ${weapon2}. There's a duel going on downstairs.

Hermione won't like it, she hates having ${weapon}s drawn under her roof, but Ron mostly just turns a blind eye and hides all the breakables before the fight starts. You wonder what happened, and if the duelists are anyone you know.

*choice
  #Go downstairs and see what the commotion is about
    *set UST +10
    Stepping into the ${pub} from the back door, you don't see what's going on at first. Everyone is crowded in the middle of the room, cheering and shouting. You can barely make out two figures in the middle of the press of bodies, going at each other with their ${weapon}s drawn.

    You sidle up to the bar, where Ron is keeping a wary eye over the proceedings. "What's going on?" you ask.

    Ron startles, so absorbed with the duel that he didn't notice your approach. "There you are!" he says. "I was waiting for you, there's been two nobles already looking for you tonight. This one," he says, nodding towards the contenders, "is more stupid than most."

    The crowd moves and you catch a glimpse of the two duelers. One of them is Zacharias Smith, who's a mediocre duelist but still has the upper hand here. The black-clad nobleman fighting him looks hopelessly outmatched.

    *page_break

    "Smith's going to kill him," Ron moans. "That means more trouble with the ${auror}s, not to mention what Hermione will say when she sees a dead body in the ${pub}." He throws you a hopeful look. "Harry, couldn't you...?"

    You roll your eyes and nod. Ron could have stopped this fight himself, but he's been making good on his promise to never duel again after his marriage.

    *goto breakoff_pub
  #Try to go back sleep despite the noise
    Stifling a yawn, you stuff your head under your pillow and try to ignore the commotion downstairs. Fortunately it doesn't last long. After a few minutes there are some louder shouts and then the ${pub} goes silent. You assume the duel is over, and fall asleep shortly after.

    You wake up in the early afternoon, feeling completely refreshed. When you go down for breakfast, Ron fills you in. Apparently some idiot nobleman walked into the ${pub} last night and provoked one of the regulars into a fight. He died in the ensuing duel, and his body was then thrown in the river. Now it's turned out that the idiot was from a wealthy and powerful family, so the ${auror}s are making inquiries.

    Ron doesn't seem too troubled as he tells you this. It's not the first time that someone has met a bad end in ${home}, and the ${auror}s have no real power here. Soon they will admit defeat and this dead nobleman will be forgotten.

    *page_break

    You, Harry Potter, will go back to your life. You will not think again about this night's events, because what do you care about a nobleman you've never even met? However, unknown to you, your star is on the decline. Among the nobles, word will get around that the man who died had come to ${home} looking for you. The nobility of ${town} will decide that you're unlucky, or dangerous, or both. With no more patrons looking to hire you, your days as a duelist are at an early end.

    [b]GAME OVER[/b]

    (And you, the player, should seriously rethink your choices. I'm going to give you a hint. This is a story about Harry Potter and Draco Malfoy, so you should at least make an attempt to meet Draco Malfoy at some point. He didn't even show up in this playthrough, he was killed offscreen! I don't know why you thought going to bed was the right option. Did you think you were going to find Malfoy naked under the covers? Tough luck, though the idea did cross my mind at least once.)
    *ending

*label breakoff_pub

You draw your ${weapon} and step between the nobleman and Zacharias. "Break it off," you say in a voice loud enough to carry over the noise in the room. "You've made your point, Zacharias, now drop this before you kill him."

Zacharias is breathing hard, more from anger than from exertion. "He needs to be taught a lesson," he snarls.

You glance at the cuts and scratches on the stranger's face. There's going to be bruises tomorrow too. "He's learned his lesson. If you want to continue," you add before he can say anything, "I'm going to claim his duel."

*page_break

Zacharias's face falls. He's not going to fight you. He lowers his ${weapon} and steps back. "Whatever, Potter," he snaps.

He retreats back among his friends, and you feel some tension easing up in your chest. There isn't going to be any more blood spilled tonight. The patrons, sensing that the entertainment is over, go back to their drinks and conversation.

Only the stranger keeps looking at you, as if unsure of what's going on. He's also painting heavily, having been fighting for his life in the past few minutes. "Potter?" he says, disbelief evident in his face. "You're the duelist?"

*page_break

You fight the urge to blush under the scrutiny. You know you're not much to look at -- short and with unruly black hair and so very common-looking. That's why you don't like to mingle with the nobs more than strictly necessary. You show up, do your job, take your money, and leave. Even if this blond stranger has seen you duel in the past, he'd have seen you at a distance and for a few minutes only. You doubt he'll recognize you.
    
However, the nobleman nods to you as if satisfied. "Yes, I remember the appalling glasses," he says.

You ignore the comment and turn towards an empty table in the corner. The stranger puts away his ${weapon} and you sit down in front of each other.

*label tavern_end

Now that the immediate threat has passed, the stranger's back to his initial cold and haughty behaviour, looking at everything down his nose.
*if (UST < 30)
  However, the effect is somewhat spoiled by the fact that he keeps wincing and gingerly poking his ribs. His pale, handsome face is shiny with sweat and his hair is sticking in every direction. His fancy black clothes are also rumpled and torn, and there's a large slash in his cape where Zacharias's ${weapon2} went through.

  By comparison, you look positively well-groomed. You relax, feeling confident and in control of this conversation already.
  *goto pub_malfoy
*else
  From close by, you can see just how handsome the stranger is. He's got fine features and high, aristocratic cheekbones. His blond hair is perfectly swept aside and his fancy black clothes are tailored in the latest fashion.

  By comparison, you look downright shabby. You fight the urge to squirm.
  *goto pub_malfoy

*label pub_malfoy

The stranger is sitting with his back ramrod-straight in his chair.

*choice
  #Ask him what's his name
    *goto introductions
  #Ask him what he's doing in ${home}
    *set Friendship %-10
    "What's a nobleman like you doing in this part of town?" you ask him.

    He glares at you. "I was looking for a famous duelist," he drawls. "But now that I found him he's not much to look at."

    You bristle at that. It's clear that he thought yours was a stupid question, you both know that he's here either to hire you or to look at you from up close. That's the only reason noblemen came here, and you feel like a fool for trying to make polite conversation with this ass.

    "Take a good look then, so you can go back to wherever you came from," you snap.

    "Not so quickly," he says. "I'm not in ${home} for the thin pleasure of your company, master duelist."

    You don't like the mocking way he says those words. "As you're well aware of, I'm Harry Potter," you snap. "You might as well use my name when talking to me. And you are?"
    *goto introductions
  #Offer him something warm to drink
    *set Friendship %+10
    "Can I get you something to drink?" you ask. It's the polite thing to say, though you suppose the stranger is rich enough to pay for his own bloody drinks.

    Sure enough, you get a cold look in response. "I wouldn't trust anything they serve here," he says loftily.

    You shrug and signal to Ron anyway, who carries two large mugs of ${butterbeer} over to your table. You take a long sip of your steaming drink, while the stranger just glares at his. But his fingers curl around it for warmth. It is, after all, a bloody cold night.

    *if (UST < 30)
      After a while he takes a careful sip and, apparently satisfied by the taste, he downs the whole thing in one large gulp. He must have been parched from his earlier exertions. Duels are thirsty work.

    "You obviously know who I am," you begin. "What's your name?"
    *goto introductions
  *if (UST < 30) #Offer him a cold compress for his bruises
    *set Friendship %+20
    The cuts on his face look painful. There is no bleeding, aside from a few drops, but the whole right side of his face is starting to swell. "Let me get you a cold compress for that," you say.

    He touches the long gash on his cheekbone gingerly. "I'm fine," he snaps, but you ignore his protests.

    *if Weapon="sword"
        You borrow a clean linen from Ron and soak it in cold water, then you wring it out and you hand it to the stranger. He eyes it with distrust, but when he presses it to his bruises, a spasm of relief crosses his face. You've been beaten up often enough in your training days to know that feeling. The stranger's posture relaxes a little.

    *if Weapon="wand"
        You borrow a clean linen from Ron and cast a cooling charm on it, then you hand the cold linen to the stranger. He eyes it with distrust, but when he presses it to his bruises, a spasm of relief crosses his face. You've been on the receiving end of enough nasty hexes in your training days to know that feeling. The stranger's posture relaxes a little.

    "So what's your name?" you ask sitting back in front of him.
    *goto introductions

*label introductions

*page_break

The stranger purses his lips. "My name is irrelevant here," he says.

You shrug. "Oh, I couldn't care less which family you're from," you say with studied nonchalance. "But it would be awkward to keep referring to you as 'the pointy-faced nobleman who can't hold a ${weapon}'."

That makes him splutter with indignation. "I'm not... My face is not...!" he says, and two spots of colour appear on his otherwise pale cheeks.

"Of course, I'll keep calling you that in my mind for lack of another name," you say, ignoring his outburst.

"Fine," he snaps. "I'm Draco Malfoy."

You vaguely remember the name as that of a very old family. His father is a member of ${parliament} and his mother a famous society hostess.

*page_break

You decide to cut to the chase. "I know why you're here, my lord Malfoy," you say, giving the words the same inflection you'd use to say 'you bumbling idiot'. "You want me to fight a duel for you. So let's hear the terms." You cross your arms and lean back in your chair, waiting.

He's taken aback by your forward question but he quickly recomposes his features. "I can assure you that you will be handsomely paid for your trouble," he says, and then he names a sum that's three times as much as what you got tonight from Lord Zabini.

The offer is very tempting, but you keep your face carefully blank. "All that for a single night's work?" you ask, raising an eyebrow. "Who would I have to fight, and why? Is it another duel over a question of honour?"

He shrugs nonchalantly. "Nothing of the sort," he says. "It's just a bit of fun for my mother's birthday party. Though I've been hard pressed to find someone willing to face you, but I'm sure someone suitable will turn up. It needs to be someone who can last more than two minutes against you..."

Your stomach sinks more and more with every word and you resist the urge to push back your chair and simply leave. Duels of honour, fought over disagreements and petty quarrels, are bad enough. But two people fighting, quite possibly to the death, simply for the entertainment of a pack of idle lords and ladies... This is disgusting.

*choice
  #Refuse politely
    *set Friendship %+10
    "I don't do birthday parties," you say. "I want all my duels to have a reason. To mean something."

    Malfoy snorts. "Even tonight at Zabini's? Wasn't that particular duel fought because someone trod on someone else's foot? That duel was also meaningless, and my ${galleon}s are just as good as his."

    You shake your head. Privately, you're intrigued that one of the nobles is criticizing their code of honour in this way: the others are almost reverent about their duels and their motivations. "It's not about the money," you insist. You like the money well enough. "It's everything else that I don't like. I can't fight a duel for no reason, against an opponent that hasn't even been named yet."
    *goto pub_refusal
  #Refuse not so politely
    *set Friendship %-10
    "I don't do birthday parties," you snarl. "I'm not going to kill someone for no better reason than to entertain some highborn assholes."

    Malfoy glares back. "What about tonight at Zabini's? Wasn't that particular duel fought because someone trod on someone else's foot? That was a bloody stupid reason for a duel, and my ${galleon}s are just as good as his," he spits out.

    How typical of them, to assume that everything could be solved by paying enough. "I don't care about your fucking money!" you yell, causing a few heads to turn your way. "You can't bribe me, Malfoy, and that's flat."
    *goto pub_refusal
  *if (UST < 30) #Point out you've already fought a duel for him tonight
    You make a joke to get out of having to explain just what about this situation doesn't sit well with you. You know that your duel of tonight was also fought for no good reason, simply because Lord Zabini had a petty quarrel with someone, but you try not to think about it. You have to draw a line somewhere, and you draw the line at birthday parties.

    "Is this about money?" Malfoy asks. "Do you want to be paid for... stepping in, back there?"

    For a moment you think he might have said 'for saving me', but Malfoy is back in control of himself now, and he's too calm and collected to admit that he was ever in any danger. Even with the bruises blooming on his cheek.

    You shake your head. "It's not about the money," you say, refusing to explain yourself any further.
    *goto pub_refusal

*label pub_refusal

*page_break

You can see that Malfoy is disappointed by your refusal. He gets up and tosses a few coins to Ron before leaving.

"If you change your mind, you'll know where to find me," he says over his shoulder. His lips are pressed together in a frown of displeasure. "The other side of the river. Anyone will point you to our town house, and there's no danger of being challenged to a duel by a stranger in [i]our[/i] neighbourhood, either."

You refrain from pointing out that, technically, he was the one who'd issued the challenge.

He goes out into the snowstorm, and you stare at the door for a long while after he's gone.

*finish A few days go by...